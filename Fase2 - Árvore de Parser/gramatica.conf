INICIO -> S
S -> INPUT
INPUT -> PROTOTYPE INPUT
INPUT -> DECLARATION INPUT
INPUT -> FUNCTION INPUT
INPUT ->
PROTOTYPE -> TYPE ID LPAREN PARAM_LIST RPAREN SEMICOLON
DECLARATION -> TYPE ID SEMICOLON
DECLARATION -> TYPE ID ASSIGN EXPRESSION SEMICOLON
DECLARATION -> TYPE ID LBRACKET EXPRESSION RBRACKET SEMICOLON
FUNCTION -> TYPE ID LPAREN PARAM_LIST RPAREN BLOCK
BLOCK -> LBRACE STATEMENT_S RBRACE
STATEMENT_S -> STATEMENT STATEMENT_S
STATEMENT_S ->
STATEMENT -> BLOCK
STATEMENT -> DECLARATION
STATEMENT -> EXPRESSION SEMICOLON
STATEMENT -> RETURN EXPRESSION SEMICOLON
STATEMENT -> IF_STATEMENT
STATEMENT -> WHILE_STATEMENT
IF_STATEMENT -> IF LPAREN EXPRESSION RPAREN BLOCK ELSE_STATEMENT
ELSE_STATEMENT -> ELSE BLOCK
ELSE_STATEMENT ->
WHILE_STATEMENT -> WHILE LPAREN EXPRESSION RPAREN STATEMENT
TYPE -> INT
TYPE -> DOUBLE
TYPE -> CHAR
TYPE -> FLOAT
TYPE -> VOID
PARAM_LIST -> PARAM_LIST_AUX
PARAM_LIST ->
PARAM_LIST_AUX -> PARAM
PARAM_LIST_AUX -> PARAM COMMA PARAM_LIST_AUX
PARAM -> TYPE ID
ASSIGN_OP -> ASSIGN
ASSIGN_OP -> PLUS_ASSIGN
ASSIGN_OP -> MINUS_ASSIGN
ASSIGN_OP -> MULT_ASSIGN
ASSIGN_OP -> DIV_ASSIGN
EXPRESSION -> B ASSIGN_OP EXPRESSION
EXPRESSION -> B
B -> B LOGICAL_OR C
B -> C
C -> C LOGICAL_AND D
C -> D
D -> D EQ E
D -> D NOT_EQUAL E
D -> E
E -> E LESS_THAN F
E -> E LESS_THAN_OR_EQUAL F
E -> E GREATER_THAN F
E -> E GREATER_THAN_OR_EQUAL F
E -> F
F -> F PLUS G
F -> F MINUS G
F -> G
G -> G MULT H
G -> G DIV H
G -> G MOD H
G -> H
H -> NOT H
H -> I
I -> I LBRACKET EXPRESSION RBRACKET
I -> LPAREN EXPRESSION RPAREN
I -> FUNCTION_CALL_OP
I -> ID
I -> INT_DECIMAL
I -> INT_OCTAL
I -> INT_HEXADECIMAL
I -> FLOAT_CONSTANT
I -> CHAR_LITERAL
I -> STRING_LITERAL
FUNCTION_CALL_OP -> ID LPAREN ARG_LIST RPAREN
ARG_LIST ->
ARG_LIST -> ARG_LIST_AUX
ARG_LIST_AUX -> EXPRESSION
ARG_LIST_AUX -> EXPRESSION COMMA ARG_LIST_AUX